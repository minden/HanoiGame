class HanoiGame {

  //The towers
  field Tower tower1;
  field Tower tower2;
  field Tower tower3;

  constructor HanoiGame new() {
    let tower1 = Tower.new(175, true);
    let tower2 = Tower.new(300, true);
    let tower3 = Tower.new(425, true);

    do tower1.push(4);
    do tower1.push(3);
    do tower1.push(2);
    do tower1.push(1);

    return this;
  }



  method void dispose() {
    do tower1.dispose();
    do tower2.dispose();
    do tower3.dispose();
    do Memory.deAlloc(this);
    return;
  }

  method void run() {
    var char key;
    var boolean exit;
    var int popTower;
    var int pushTower;
    var int counter;
    let exit = false;

    while (~exit) {


        while (key = 0) {
            let key = Keyboard.keyPressed();

        }
        do Screen.setColor(false);
        do Screen.drawRectangle(0, 10, 50, 55);
        do Screen.setColor(true);

        do Output.moveCursor(3,1);
        do Output.printString("From: ");
        do Output.printChar(key);

        if (key = 81) {
            let exit = true;
        }
        if (key = 49) {
            let popTower = 1;
        }
        if (key = 50) {
            let popTower = 2;
        }
        if (key = 51) {
            let popTower = 3;
        }

        while (~(key = 0)) {
            let key = Keyboard.keyPressed();
        }

        while (key = 0) {
            let key = Keyboard.keyPressed();
        }

        do Output.moveCursor(4,1);
        do Output.printString("To: ");
        do Output.printChar(key);

        let counter = counter + 1;
        do Output.moveCursor(21,1);
        do Output.printString("Counter: ");
        do Output.printInt(counter);


        if (key = 81) {
            let exit = true;
        }
        if (key = 49) {
            let pushTower = 1;
        }
        if (key = 50) {
            let pushTower = 2;
        }
        if (key = 51) {
            let pushTower = 3;
        }

        // waits for the key to be released.
        while (~(key = 0)) {
            let key = Keyboard.keyPressed();
        }
        do switchplate(popTower, pushTower);
        if (tower2.finished()){
          do finishScreen(counter);
          let exit = true;

        }
        if (tower3.finished()){
          do finishScreen(counter);
          let exit = true;

        }
      }

      return;
    }


    method void switchplate (int popTower, int pushTower){
      var int popedPlate;
      var Tower popTowerName;
      var Tower pushTowerName;
      var int popTowerHeight;
      var int pushTowerHeight;

      if (popTower = 1) {let popTowerName = tower1;}
      if (popTower = 2) {let popTowerName = tower2;}
      if (popTower = 3) {let popTowerName = tower3;}

      if (pushTower = 1) {let pushTowerName = tower1;}
      if (pushTower = 2) {let pushTowerName = tower2;}
      if (pushTower = 3) {let pushTowerName = tower3;}

      let popTowerHeight = popTowerName.peek();
      let pushTowerHeight = pushTowerName.peek();


      if (popTowerHeight < pushTowerHeight){
        let popedPlate =  popTowerName.pop();
        do pushTowerName.push(popedPlate);
      }
      else {
        if(popTowerHeight = 100) {
          do Output.moveCursor(6,1);
          do Output.printInt(popTower);
          do Output.printString(" is empty!");
        }
        else {
          do Output.moveCursor(6,1);
          do Output.printString("The plate on: ");
          do Output.printInt(popTower);
          do Output.moveCursor(7,1);
          do Output.printString("is bigger then");
          do Output.moveCursor(8,1);
          do Output.printString("the plate on: ");
          do Output.printInt(pushTower);
        }
      }


      return;

    }

    method void finishScreen(int counter){

      var char key;

      do Screen.clearScreen();
      do Output.moveCursor(10,20);
      do Output.printString("Wow, you made it in ");
      do Output.printInt(counter);
      do Output.printString(" steps!!!");

      do Output.moveCursor(15,25);
      do Output.printString("Press any key to restart.");

      while (key = 0) {
          let key = Keyboard.keyPressed();
      }

      do Sys.wait(300);

      return;

    }


}
