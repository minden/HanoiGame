class HanoiGame {

  //The towers
  field Tower tower1;
  field Tower tower2;
  field Tower tower3;

  constructor HanoiGame new() {
    let tower1 = Tower.new(175, true);
    let tower2 = Tower.new(300, true);
    let tower3 = Tower.new(425, true);

    do tower1.push(4);
    do tower1.push(3);
    do tower1.push(2);
    do tower1.push(1);

    return this;
  }

  method void dispose() {
    do tower1.dispose();
    do tower2.dispose();
    do tower3.dispose();
    do Memory.deAlloc(this);
    return;
  }

  method void run() {
    var char key;
    var boolean exit;
    var int popTower;
    var int pushTower;
    var int counter;
    let exit = false;

    do startScreen();
    do introductionScreen();

    do Screen.clearScreen();
    do Output.moveCursor(1,25);
    do Output.printString("Tower of Hanoi");

    do Screen.drawLine(0,45, 511, 45);
    do Screen.drawLine(120,45, 120, 255);

    do tower1.draw();
    do tower2.draw();
    do tower3.draw();

    while (~exit) {

        while (key = 0) {
            let key = Keyboard.keyPressed();

        }
        do Screen.setColor(false);
        do Screen.drawRectangle(0, 46, 60, 80);
        do Screen.setColor(true);

        do Output.moveCursor(5,1);
        do Output.printString("From: ");
        do Output.printChar(key);

        if (key = 81) {
            let exit = true;
        }
        if (key = 49) {
            let popTower = 1;
        }
        if (key = 50) {
            let popTower = 2;
        }
        if (key = 51) {
            let popTower = 3;
        }

        while (~(key = 0)) {
            let key = Keyboard.keyPressed();
        }

        while (key = 0) {
            let key = Keyboard.keyPressed();
        }

        do Output.moveCursor(6 ,1);
        do Output.printString("To: ");
        do Output.printChar(key);

        let counter = counter + 1;
        do Output.moveCursor(21,1);
        do Output.printString("Counter: ");
        do Output.printInt(counter);


        if (key = 81) {
            let exit = true;
        }
        if (key = 49) {
            let pushTower = 1;
        }
        if (key = 50) {
            let pushTower = 2;
        }
        if (key = 51) {
            let pushTower = 3;
        }

        // waits for the key to be released.
        while (~(key = 0)) {
            let key = Keyboard.keyPressed();
        }
        do switchplate(popTower, pushTower);
        if (tower2.finished()){
          do finishScreen(counter);
          let exit = true;

        }
        if (tower3.finished()){
          do finishScreen(counter);
          let exit = true;

        }
      }

      return;
    }


    method void switchplate (int popTower, int pushTower){
      var int popedPlate;
      var Tower popTowerName;
      var Tower pushTowerName;
      var int popTowerHeight;
      var int pushTowerHeight;

      if (popTower = 1) {let popTowerName = tower1;}
      if (popTower = 2) {let popTowerName = tower2;}
      if (popTower = 3) {let popTowerName = tower3;}

      if (pushTower = 1) {let pushTowerName = tower1;}
      if (pushTower = 2) {let pushTowerName = tower2;}
      if (pushTower = 3) {let pushTowerName = tower3;}

      let popTowerHeight = popTowerName.peek();
      let pushTowerHeight = pushTowerName.peek();


      if (popTowerHeight < pushTowerHeight){
        let popedPlate =  popTowerName.pop();
        do pushTowerName.push(popedPlate);
      }
      else {
        if(popTowerHeight = 100) {
          do Output.moveCursor(6,1);
          do Output.printInt(popTower);
          do Output.printString(" is empty!");
        }
        else {
          do Output.moveCursor(8,1);
          do Output.printString("Plate on: ");
          do Output.moveCursor(9,1);
          do Output.printInt(popTower);
          do Output.moveCursor(10,1);
          do Output.printString("is bigger then");
          do Output.moveCursor(11,1);
          do Output.printString("the plate on: ");
          do Output.moveCursor(12,1);
          do Output.printInt(pushTower);
        }
      }


      return;

    }

    method void startScreen(){
      var int i;
      do Screen.clearScreen();
      let i = 20;

      while (i > 0){
        do Output.moveCursor(i,25);
        do Output.printString("Tower of Hanoi");
        do Sys.wait(100);
        do Screen.clearScreen();
        do Sys.wait(50);
        let i = i-1;
      }


      return;
    }

    method void introductionScreen(){
      var int i;

      do Screen.clearScreen();
      do Output.moveCursor(1,25);
      do Output.printString("Tower of Hanoi");


      do Output.moveCursor(6,0);
      do Output.printString("How to play:");
      do Output.moveCursor(8,0);
      do Output.printString("First choose the tower you want to move the plate away from");
      do Output.moveCursor(9,0);
      do Output.printString("by pressing a number between 1-3.");
      do Output.moveCursor(11, 0);
      do Output.printString("Then choose the destination tower");
      do Output.moveCursor(12,0);
      do Output.printString("by pressing a number between 1-3.");
      do Output.moveCursor(14,0);
      do Output.printString("The goal is to build the exact same stack") ;
      do Output.moveCursor(15,0);
      do Output.printString("as you see on tower 1 on tower 2 or 3.");

      let i = 9;

      while (i>0){
        do Output.moveCursor (20,30);
        do Output.printInt(i);
        do Sys.wait(1000);
        let i = i-1;
      }

      return;
    }

    method void finishScreen(int counter){

      var char key;

      do Screen.clearScreen();
      do Output.moveCursor(10,15);

      if(counter = 15){
        do Output.printString("You ROCK! You made it in ");
        do Output.printInt(counter);
        do Output.printString(" steps!");
        do Output.moveCursor(11,15);
        do Output.printString(" That is the smallest amount possible!");
      }

      if(counter < 20 & counter > 15){
        do Output.printString("Wow, you made it in ");
        do Output.printInt(counter);
        do Output.printString(" steps!!!");
      }

      if(counter > 19){
        do Output.printString("Come on,  ");
        do Output.printInt(counter);
        do Output.printString(" steps? You can do better!");
      }


      do Output.moveCursor(15,20);
      do Output.printString("Press any key to restart.");

      while (key = 0) {
          let key = Keyboard.keyPressed();
      }

      do Sys.wait(300);

      return;

    }

}
